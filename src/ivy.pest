// basic tokenization
char = { ASCII_ALPHANUMERIC | "_" | ":" }
WHITESPACE = _{ " " | "\t" | NEWLINE }
// periods can only be in the middle of identifiers
// (to disambiguate the period in forall T. e)
ident = @{ char+ ~ ("." ~ char+)* }

// the top-level rule
file = { SOI ~ step_def* ~ EOI }

step_def = { ident ~ "=" ~  action }
action = { "action" ~ ("(" ~ ident ~ ")")? ~ rule_block }
rule_block_or_step = _{ rule_block | rule_step }
rule_block = {
  // an empty rule block
  "{" ~ "}"
  | "{" ~ rule_block_or_step ~
    (";" ~ rule_block_or_step)* ~ "}"
}

rule_step = { assign_rule | assume_rule | assert_rule | if_rule }

relation = _{ call_expr | ident }
assign_rule = { relation ~ ":=" ~ expr }
assume_rule = {  "assume" ~ expr }
assert_rule = {  "assert" ~ expr }
if_rule = {
  "if" ~ expr ~ rule_block_or_step ~
  ("else" ~ rule_block_or_step)?
}

expr = { prefix* ~ term ~ (infix ~ prefix* ~ term)* }

// the infix/prefix operators
infix = _{ and | or | implies | iff | equal }
and = { "&" }
or = { "|" } // is this even the syntax?
implies = { "->" }
iff = { "<->" }
equal = { "=" }
prefix = _{ not }
not = { "~" }

// some other expression constructs
call_expr = { ident ~ "(" ~ ident ~ ")" }
forall_expr = { "forall" ~ ident  ~ "." ~ expr }
some_expr = { "some" ~ ident  ~ "." ~ expr }
havoc_expr = { "*" }
base_expr =  _{ forall_expr | some_expr | havoc_expr | relation }
term = _{ base_expr | "(" ~ expr ~ ")" }
